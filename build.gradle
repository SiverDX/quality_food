buildscript {
    repositories {
        maven { url 'https://maven.minecraftforge.net' }
        maven { url 'https://repo.spongepowered.org/repository/maven-public/' }
        mavenCentral()
    }

    dependencies {
        classpath group: 'org.spongepowered', name: 'mixingradle', version: '0.7+'
    }
}

plugins {
    id 'net.minecraftforge.gradle' version '[6.0,6.2)'
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'
}

apply plugin: 'org.spongepowered.mixin'

version = minecraft_version + "-" + mod_version

minecraft {
    mappings channel: mapping_channel, version: mapping_version

    copyIdeResources = true

    // When true, this property will add the folder name of all declared run configurations to generated IDE run configurations.
    // The folder name can be set on a run configuration using the "folderName" property.
    // By default, the folder name of a run configuration is the name of the Gradle project containing it.
    // generateRunFolders = true

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        configureEach {
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }

        client {
            workingDirectory project.file('run/client')
            property 'forge.enabledGameTestNamespaces', mod_id
        }

        clientRandom {
            parent runs.client
            // #### will be replaced with a random number | UUID is a randomly generated one
            args '--username', 'Dev####', '--uuid', 'fdb0e318-1ac3-4049-b6f8-d024c0b87a56'
        }

        server {
            workingDirectory project.file('run/server')
            property 'forge.enabledGameTestNamespaces', mod_id
            // args '--nogui'
        }

        data {
            workingDirectory project.file('run-data')
            args '--mod', mod_id, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
        }
    }
}

mixin {
    add sourceSets.main, "${mod_id}.refmap.json"
    config "${mod_id}.mixins.json"
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    flatDir { dirs 'dev' }
    maven { url = "https://jitpack.io" } // Mixin Squared
    maven { url = "https://www.cursemaven.com" } // CurseForge
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    /* --- Compatibility --- */
    implementation fg.deobf("curse.maven:jade-324717:4914105") // 8.9.2

    implementation fg.deobf("curse.maven:farmers-delight-398521:5051241") // 1.2.4

    compileOnly fg.deobf("curse.maven:fastentitytransfer-828407:5202135") // 1.3.1

    compileOnly fg.deobf("curse.maven:harvest-with-ease-602171:5384545") // 9.0.0.3

    compileOnly fg.deobf("curse.maven:sophisticated-core-618298:5296313") // 0.6.4.605
    compileOnly fg.deobf("curse.maven:sophisticated-backpacks-422301:5194759") // 3.20.2.1035
    compileOnly fg.deobf("curse.maven:sophisticated-storage-619320:5194750") // 0.9.7.765

    compileOnly fg.deobf("curse.maven:toms-storage-378609:5211204") // 1.5.10

    compileOnly fg.deobf("curse.maven:create-328085:4835190") // 0.5.1f

    compileOnly fg.deobf("curse.maven:wonderful-enchantments-417439:5177126") // 1.10.7
    compileOnly fg.deobf("curse.maven:majrusz-library-437409:5177143") // 7.0.5

    compileOnly fg.deobf("curse.maven:lets-do-vinery-704465:4833017") // 1.3.12b
    compileOnly fg.deobf("curse.maven:do-api-864599:4676882") // 1.1.0b
    compileOnly fg.deobf("curse.maven:architectury-api-419699:5137942") // 6.6.92

    compileOnly fg.deobf("curse.maven:quark-243121:4812006") // 3.4-418
    compileOnly fg.deobf("curse.maven:autoreglib-250363:4100299") // 1.8.2-55

    compileOnly fg.deobf("curse.maven:rightclickharvest-452834:4690530") // 3.2.3

    compileOnly fg.deobf("curse.maven:supplementaries-412082:5458843") // 2.8.17
    compileOnly fg.deobf("curse.maven:selene-499980:5478857") // 2.12.6

    /* --- Test --- */
    implementation fg.deobf("curse.maven:appleskin-248787:3872808")

    /* --- Mixin Configuration --- */
    implementation(annotationProcessor("io.github.llamalad7:mixinextras-common:${mixin_extras_version}"))
    implementation(jarJar("io.github.llamalad7:mixinextras-forge:${mixin_extras_version}")) {
        jarJar.ranged(it, "[${mixin_extras_version},)")
    }

    annotationProcessor "org.spongepowered:mixin:0.8.5:processor"
}

tasks.named('processResources', ProcessResources).configure {
    var replaceProperties = [
            minecraft_version: minecraft_version,
            forge_version    : forge_version,
            mod_id           : mod_id,
            mod_name         : mod_name,
            mod_license      : mod_license,
            mod_version      : mod_version,
            mod_author       : mod_author,
            mod_description  : mod_description,
    ]

    inputs.properties replaceProperties

    filesMatching(['META-INF/mods.toml', 'pack.mcmeta']) {
        expand replaceProperties + [project: project]
    }
}

tasks.named('jar', Jar).configure {
    manifest {
        attributes([
                "Specification-Title"     : mod_id,
                "Specification-Vendor"    : mod_author,
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : mod_version,
                "Implementation-Vendor"   : mod_author,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }

    finalizedBy 'reobfJar'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}